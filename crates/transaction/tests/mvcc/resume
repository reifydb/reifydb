# Copyright (c) reifydb.com 2025
# This file is licensed under the AGPL-3.0-or-later

# This file includes and modifies code from the toydb project (https://github.com/erikgrinaker/toydb),
# originally licensed under the Apache License, Version 2.0.
# Original copyright:
#   Copyright (c) 2024 Erik Grinaker
#
# The original Apache License can be found at:
#   http://www.apache.org/licenses/LICENSE-2.0

# Resume should resume a transaction with the same state.

# Commit some visible values.
t1: begin
t1: set a=1 b=1
t1: commit
---
ok

# We then start three transactions, of which we will resume t3.  We commit t2
# and t4's changes, which should not be visible, and write a change for t3 which
# should be visible.
t2: begin
t3: begin
t4: begin
---
ok

t2: set a=2
t3: set b=3
t4: set c=4
t2: commit
t4: commit
---
ok

# We now resume t3 as t5.
t3: state
---
t3: v3 rw active={2}

t5: resume '{"version":3, "read_only":false, "active":[2]}'
t5: state
---
t5: v3 rw active={2}

# t5 can see its own changes, but not the others.
t5: scan
---
t5: "a" → "1"
t5: "b" → "3"

# A new transaction should not see t3/5's uncommitted changes.
t6: begin
t6: scan
---
t6: "a" → "2"
t6: "b" → "1"
t6: "c" → "4"

# Once t5 commits, a separate transaction should see its changes.
t5: commit [ops]
---
t5: engine remove mvcc:TxWrite(3, "b") ["\x03\x00\x00\x00\x00\x00\x00\x00\x03b\x00\x00"]
t5: engine remove mvcc:TxActive(3) ["\x01\x00\x00\x00\x00\x00\x00\x00\x03"]

t7: begin
t7: scan
---
t7: "a" → "2"
t7: "b" → "3"
t7: "c" → "4"

# Resuming a committed transaction should error.
t8: !resume '{"version":3, "read_only":false, "active":[2]}'
---
t8: Error: invalid input: no active transaction at version 3

# It should also be possible to start a snapshot transaction in t3 and resume
# it. It should not see t3's writes, nor t2's.
t8: begin readonly as_of=3
t8: state
---
t8: v3 ro active={2}

t8: scan
---
t8: "a" → "1"
t8: "b" → "1"

t9: resume '{"version":3, "read_only":true, "active":[2]}'
t9: state
---
t9: v3 ro active={2}

t9: scan
---
t9: "a" → "1"
t9: "b" → "1"
